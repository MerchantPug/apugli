plugins {
    id 'java'
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
    id "com.github.breadmoirai.github-release" version "2.4.1"
}

archivesBaseName = "${mod_name}"
version = "${rootProject.version}+${minecraft_version}-common"

repositories {
    maven {
        name = 'Sponge / Mixin'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven {
        name = "JitPack"
        url = 'https://jitpack.io'
    }
    maven {
        name = "Cafeteria"
        url = 'https://maven.cafeteria.dev'
        content {
            includeGroup 'net.adriantodt.fabricmc'
        }
    }
    maven {
        name = "Jamie's White Shirt"
        url "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        name = "Shedaniel"
        url "https://maven.shedaniel.me/"
    }
    maven {
        name = "TerraformersMC"
        url "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    mavenCentral()
}

loom {
    mixin {
        useLegacyMixinAp = false
    }
    accessWidenerPath = file("src/main/resources/apugli.common.accesswidener")
}

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.19.3:${parchment_version}@zip")
    }
    //Apoli
    modCompileOnly "com.github.apace100:apoli:${apoli_version}"
    //MixinExtras
    annotationProcessor(compileOnly("com.github.llamalad7.mixinextras:mixinextras-common:${mixin_extras_version}"))
    //JSR305
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
            artifact sourcesJar
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials(PasswordCredentials)
            authentication {
                create("basic", BasicAuthentication)
            }
        }
        mavenLocal()
    }
}

githubRelease {
    if (System.getenv("GITHUB_TOKEN") == null) return
    token = System.getenv("GITHUB_TOKEN")
    owner = gh_owner
    repo = gh_repo
    tagName = "${rootProject.version}+${minecraft_version}"
    targetCommitish = gh_commitish
    releaseName = "$project.mod_name v${rootProject.version}+${minecraft_version}"
    body {
        new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    }
}

tasks.remapJar {
    enabled = false
}

tasks.remapSourcesJar {
    enabled = false
}